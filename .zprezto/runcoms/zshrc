#
# Executes commands at the start of an interactive session.  Authors: Sorin Ionescu <sorin.ionescu@gmail.com>
# 
# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
    source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...
if [[ $(uname) == 'Linux' ]]; then
    export OS=linux
elif [[ $(uname) == 'Darwin' ]]; then
    export OS=macos
fi

if [[ -f ~/.lesskey ]]; then
    lesskey
fi

if [[ -f /usr/share/doc/pkgfile/command-not-found.zsh ]]; then
    source /usr/share/doc/pkgfile/command-not-found.zsh
fi

export NVM_DIR="/home/enzime/.nvm"

if [[ -s $NVM_DIR/nvm.sh ]]; then
    source $NVM_DIR/nvm.sh
fi

if [[ -s /usr/bin/virtualenvwrapper.sh ]]; then
    source /usr/bin/virtualenvwrapper.sh

    export WORKON_HOME=~/.venvs
    mkdir -p $WORKON_HOME
fi

alias _="sudo"
alias sudo="sudo "
alias md="mkdir"
alias pacman="pacmatic"

if [[ $OS == 'linux' ]]; then
    alias ls="ls -F --color=auto"
elif [[ $OS == 'macos' ]]; then
    alias ls="ls -FG"
fi

alias l="ls -lah"
alias mr="req_ssh_agent mr"
alias mp="mpv --no-video"
alias ranger="ranger-cd"
alias v="vcsh"

alias gai="git add --interactive"
alias gca="git commit --amend"
alias gco="git checkout --patch"
alias gC="git checkout"
alias gD="git diff"
alias gDs="gD --cached"
alias gln="gl -n"
alias gRv="gR -v"
alias gs="git status"
alias gss="git stash save -p"
alias gsS="git stash save --include-untracked"
alias gS="git show"

# fasd
alias a="fasd -a"
alias c="fasd_cd -d"
alias di="fasd -d"
alias f="fasd -f"
alias m="fasd -e mpv -f"
# alias v="fasd -e vim -f"

export VISUAL=nvim
export EDITOR=nvim

function req_ssh_agent {
    if [[ -z $SSH_AGENT_PID ]]; then
        eval $(ssh-agent)
    fi

    if [[ $(ssh-add -l) = "The agent has no identities." ]]; then
        ssh-add ~/.ssh/id_rsa
    fi

    $@
}

function ga {
    if [[ -z $1 ]]; then
        git add -p
    else
        git add $@
    fi
}

function gps {
    branch=$(git rev-parse --abbrev-ref HEAD) || return 1
    if [[ $branch != "HEAD" ]]; then
        git push --set-upstream fork $branch
    else
        echo "Not on a branch"
        return 1
    fi
}

function gt {
    git --no-pager diff --binary --no-color | grep -i '^\+.*todo'
}

function gts {
    git --no-pager diff --binary --no-color --cached | grep -i '^\+.*todo'
}

function gtu {
    git --no-pager diff --binary --no-color ${1:-origin/master}...${2:-HEAD} | grep -i '^\+.*todo'
}

function require_vcsh_repository {
    if [[ -z $1 ]]; then
        echo "vcsh requires a repository"
        return 1
    fi
}

function va {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) ga ${@:2}
}


function vc {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gc ${@:2}
}

function vC {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gC ${@:2}
}

function vD {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gD ${@:2}
}

function vDs {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gDs ${@:2}
}

function vm {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gm ${@:2}
}

function vs {
    if [[ -z $1 ]]; then
        vcsh status
    else
        vcsh $1 status ${@:2}
    fi
}

function vss {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gss ${@:2}
}

function vp {
    if [[ -z $1 ]]; then
        req_ssh_agent vcsh push
    else
        vcsh $1 push ${@:2}
    fi
}

function vpf {
    require_vcsh_repository $@ || return 1

    GIT_DIR=$(vcsh $1 rev-parse --git-dir) gpf ${@:2}
}

function vf {
    if [[ -z $1 ]]; then
        vcsh foreach fetch
    else
        vcsh $1 fetch ${@:2}
    fi
}

function folder-chooser {
    VISUAL=ranger-folder-selector zle edit-command-line
}

function file-chooser {
    VISUAL=ranger-file-selector zle edit-command-line
}

function ranger-cd {
    tempfile=$(mktemp)
    \ranger --choosedir="$tempfile" "${@:-$(pwd)}" < $TTY
    test -f "$tempfile" &&
    if [[ "$(cat -- "$tempfile")" != "$(echo -n `pwd`)" ]]; then
        cd -- "$(cat "$tempfile")"
    fi
    rm -f -- "$tempfile"
}

function carry-ranger {
    \ranger < $TTY
    VISUAL=true zle edit-command-line
}

function carry-ranger-cd {
    ranger-cd
    VISUAL=true zle edit-command-line
}

# peco
function exists { which $1 &> /dev/null }

if exists peco; then
    function peco_select_history() {
        local tac
        exists gtac && tac="gtac" || { exists tac && tac="tac" || { tac="tail -r" } }
        local peco
        [[ -z "$LBUFFER" ]] && peco="peco" || peco='peco --query "$LBUFFER"'
        BUFFER=$(fc -l -n 1 | eval $tac | eval $peco)
        CURSOR=$#BUFFER         # move cursor
        zle -R -c               # refresh
    }

    zle -N peco_select_history
    bindkey '^R' peco_select_history
fi

function volume-control {
    VISUAL=alsamixer zle edit-command-line
}

function open-htop {
    VISUAL=htop zle edit-command-line
}

autoload -z edit-command-line
zle -N edit-command-line

zle -N folder-chooser
zle -N file-chooser
zle -N carry-ranger
zle -N carry-ranger-cd
zle -N volume-control
zle -N open-htop

stty -ixon

bindkey '^E^E' edit-command-line
bindkey '^Ed' folder-chooser
bindkey '^Ef' file-chooser
bindkey '^Er' carry-ranger
bindkey '^Ec' carry-ranger-cd
bindkey '^Ea' volume-control
bindkey '^Eh' open-htop
bindkey -s '^Et' 'tt^M'
bindkey -s ',R' 'source ~/.zshrc^M'

bindkey -r '^[OA'
bindkey -r '^[OB'
bindkey -r '^N'
bindkey -r '^P'
bindkey -r '^S'

bindkey '^H' emacs-backward-word
bindkey '^J' history-substring-search-down
bindkey '^K' history-substring-search-up
bindkey '^L' emacs-forward-word

export QT_STYLE_OVERRIDE=GTK+
export STEAM_FRAME_FORCE_CLOSE=1
